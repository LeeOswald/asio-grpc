# Copyright 2021 Dennis Hezel
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

execute_process(
    COMMAND "@GIT_EXECUTABLE@" "diff-index" "--cached" "--name-only" "--diff-filter=ACMRT" "HEAD"
    OUTPUT_VARIABLE _git_output
    WORKING_DIRECTORY "@CMAKE_SOURCE_DIR@")

# process CMake files
string(REGEX MATCHALL "cmake.*\\.cmake.in|cmake.*\\.cmake|CMakeLists.txt" _git_output_only_cmake_files "${_git_output}")

foreach(_file IN LISTS _git_output_only_cmake_files)
    execute_process(COMMAND "@ASIO_GRPC_CMAKE_FORMAT_PROGRAM@" "-i" "${_file}" WORKING_DIRECTORY "@CMAKE_SOURCE_DIR@")

    execute_process(COMMAND "@GIT_EXECUTABLE@" "add" "${_file}" WORKING_DIRECTORY "@CMAKE_SOURCE_DIR@")
endforeach()

# process cpp files
string(REGEX MATCHALL ".*(\\.hpp|\\.ipp|\\.cpp|\\.proto)" _git_output_only_cpp_files "${_git_output}")

foreach(_file IN LISTS _git_output_only_cpp_files)
    execute_process(COMMAND "@ASIO_GRPC_CLANG_FORMAT_PROGRAM@" "-i" "-style=file" "${_file}"
                    WORKING_DIRECTORY "@CMAKE_SOURCE_DIR@")

    execute_process(COMMAND "@GIT_EXECUTABLE@" "add" "${_file}" WORKING_DIRECTORY "@CMAKE_SOURCE_DIR@")
endforeach()
